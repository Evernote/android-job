apply plugin: 'com.android.application'
apply from: '../build-config/gradle-quality.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.evernote.android.job.demo"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles 'proguard.cfg'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
            signingConfig signingConfigs.debug
        }

        gcm.initWith(buildTypes.debug)
        gcm {
            applicationIdSuffix ".gcm"
            matchingFallbacks = ['debug', 'release']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError true
    }
}

dependencies {
    implementation project(':library')
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "com.facebook.stetho:stetho:$stethoVersion"

    gcmImplementation "com.google.android.gms:play-services-gcm:$playServicesVersion"
    gcmImplementation "androidx.work:work-runtime:$workVersion"
}

//uploadArchives.enabled false